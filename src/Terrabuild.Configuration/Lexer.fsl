{
// module Lexer

open Parser.HCL  // we need the terminal tokens from the Parser
open FSharp.Text.Lexing
open System.Text
open Errors

let lexeme = LexBuffer<_>.LexemeString

let mkIdentifier lexbuf =
    let s = lexbuf |> lexeme  |> string
    s.Replace("`", "").Replace(" ", "").Replace(":", "").Replace("$", "")

[<RequireQualifiedAccess>]
type LexerMode =
    | Default
    | String

let lexerMode = System.Collections.Generic.Stack([LexerMode.Default])


}

let regularIdentifier = ['a'-'z'] (['_' '-']? ['a'-'z' '0'-'9']+)*
let extensionIdentifier = '@' regularIdentifier
let targetIdentifier = '^' regularIdentifier

let identifier = regularIdentifier | extensionIdentifier | targetIdentifier
let keyIdentifier = regularIdentifier ':'

let number = '-'? ['0'-'9']+('.'['0'-'9']+)?
let whitespace = ' ' | '\t'
let newline = '\n' | '\r' '\n'





rule token = parse
    | "??" { DOUBLE_QUESTION }
    | "?" { QUESTION }
    | ".?" { DOT_QUESTION }
    | "." { DOT }
    | ":" { COLON }
    | "{" {
        let mode = lexerMode.Peek()
        lexerMode.Push(LexerMode.Default)
        match mode with
        | LexerMode.String -> STRING_START
        | _ -> LBRACE
    }
    | "}" {
        lexerMode.Pop() |> ignore
        match lexerMode.Peek() with
        | LexerMode.Default -> RBRACE
        | _ -> EXPRESSION_END
    }
    | "[" { LSQBRACKET }
    | "]" { RSQBRACKET }
    | "(" { LPAREN }
    | ")" { RPAREN }
    | "==" { DOUBLE_EQUAL }
    | "=" { EQUAL }
    | "!=" { NOT_EQUAL }
    | "," { COMMA }
    | "-" { MINUS }
    | "+" { PLUS }
    | "!" { BANG }
    | "&" { AND }
    | "|" { OR }
    | "#" { singleLineComment lexbuf }
    | '"' {
        lexerMode.Push(LexerMode.String)
        STRING_START
    }

    | identifier { lexbuf |> mkIdentifier |> IDENTIFIER }
    | keyIdentifier { lexbuf |> mkIdentifier |> KEY }

    | number {
        let s = lexeme lexbuf |> int
        NUMBER (s)
    }

    | whitespace { token lexbuf }
    | newline { lexbuf.EndPos <- lexbuf.EndPos.NextLine; token lexbuf }
    | eof { EOF }
    | _ { failwithf "unrecognized input: '%s'" <| lexeme lexbuf }

and singleLineComment = parse
    | newline { lexbuf.EndPos <- lexbuf.EndPos.NextLine; token lexbuf }
    | eof { EOF }
    | _ { singleLineComment lexbuf }

and interpolatedString (acc: StringBuilder) = parse
    | newline { raiseParseError "newline encountered in string" }
    | "\"\"" {
        acc.Append("\"") |> ignore
        interpolatedString acc lexbuf
    }
    | "{{" {
        acc.Append("{") |> ignore
        interpolatedString acc lexbuf
    }
    | "}}" {
        acc.Append("}") |> ignore
        interpolatedString acc lexbuf
    }
    | '"' {
        lexerMode.Pop() |> ignore
        STRING_END (acc.ToString())
    }
    | '{' {
        lexerMode.Push(LexerMode.Default)
        EXPRESSION_START (acc.ToString())
    }
    | _ {
        lexbuf |> lexeme |> acc.Append |> ignore
        interpolatedString acc lexbuf
    }
